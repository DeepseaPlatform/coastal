plugins {
	id 'application'
	id 'java'
	id 'eclipse'
	id 'com.palantir.git-version' version '0.12.0-rc2'
	id 'checkstyle'
}

mainClassName = 'za.ac.sun.cs.coastal.COASTAL'

repositories {
	jcenter()
	maven { url 'http://repo.jacogeld.org:8081/artifactory/gradle-dev-local/' }
	maven { url 'https://plugins.gradle.org/m2/' }
}

dependencies {
	compile group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.3'
	compile group: 'commons-io', name: 'commons-io', version: '2.6'
	compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.10.0'
	compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.10.0'
	compile group: 'org.apache.commons', name: 'commons-configuration2', version: '2.4'
	compile group: 'org.ow2.asm', name: 'asm', version: '7.0'
	compile group: 'org.ow2.asm', name: 'asm-commons', version: '7.0'
	compile group: 'org.ow2.asm', name: 'asm-util', version: '7.0'
	compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '5.1.3.201810200350-r'
	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile group: 'org.slf4j', name: 'slf4j-nop', version: '1.7.25'
}

// So that XML files in src/test/java/tests/* are copied
// to the test resources directory.
sourceSets.test.resources.srcDirs += [ 'src/test/java' ]

// Enforce gradle version for Travis CI
task wrapper(type: Wrapper) {
	gradleVersion = '4.3'
}
	 
checkstyle {
	toolVersion '8.1'
}

javadoc {
	// options.memberLevel = JavadocMemberLevel.PRIVATE
	options.noQualifiers = [ 'java.lang.*', 'java.io.*', 'java.util.*', 'org.apache.*' ]
}

version = gitVersion()
processResources {
    filesMatching('**/app.properties') { filter { it.replace('%VERSION%', version) } }
}
