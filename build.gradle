plugins {
	id 'application'
	id 'java'
	id 'eclipse'
	id 'checkstyle'
}

mainClassName = 'za.ac.sun.cs.coastal.COASTAL'

repositories {
	jcenter()
	maven { url 'http://repo.jacogeld.org:8081/artifactory/gradle-dev-local/' }
	maven { url 'https://plugins.gradle.org/m2/' }
}

dependencies {
	compile group: 'za.ac.sun.cs', name: 'green', version: '0.1.5-SNAPSHOT'
	compile group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.3'
	compile group: 'commons-io', name: 'commons-io', version: '2.6'
	compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.10.0'
	compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.10.0'
	compile group: 'org.apache.commons', name: 'commons-configuration2', version: '2.4'
	compile group: 'org.ow2.asm', name: 'asm', version: '7.0'
	compile group: 'org.ow2.asm', name: 'asm-commons', version: '7.0'
	compile group: 'org.ow2.asm', name: 'asm-util', version: '7.0'
	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile group: 'org.slf4j', name: 'slf4j-nop', version: '1.7.25'
	implementation files(org.gradle.internal.jvm.Jvm.current().toolsJar)
}

// So that XML files in src/test/java/tests/* are copied
// to the test resources directory.
sourceSets.test.resources.srcDirs += [ 'src/test/java' ]

// Enforce gradle version for Travis CI
task wrapper(type: Wrapper) {
	gradleVersion = '4.3'
}
	 
checkstyle {
	toolVersion '8.1'
}

javadoc {
	destinationDir = file('build/docs/api')
	options.docletpath = [ file('build/classes/java/main') ]
	options.doclet = 'za.ac.sun.cs.coastal.utility.Doclet'
	options.noQualifiers = [ 'java.lang.*', 'java.io.*', 'java.util.*', 'org.apache.*' , 'za.ac.sun.cs.coastal.' ]
	// 'org.apache.logging.log4j.*', 'org.apache.commons.configuration2.*'
	options.memberLevel = JavadocMemberLevel.PRIVATE
}

run {
    if (project.hasProperty('args'))
       args findProperty('args')
}
