//==============================================================================
// Gradle build file for COASTAL
//==============================================================================

plugins {
	id 'application'
	id 'java'
	id 'eclipse'
	id 'checkstyle'
	id 'maven-publish'
	id 'signing'
	id 'com.peterabeles.gversion' version '1.5.2' // for compile-time info
	// id 'de.set.ecj' version '1.4.1' // for using the eclipse compiler
}

group = 'com.github.DeepseaPlatform'
mainClassName = 'za.ac.sun.cs.coastal.COASTAL'
version = '0.2.0-SNAPSHOT'

ext.isReleaseVersion = !version.endsWith('SNAPSHOT')

//------------------------------------------------------------------------------
// Repositories and dependencies
//------------------------------------------------------------------------------

repositories {
	jcenter()
	maven { url 'https://plugins.gradle.org/m2/' }
}

dependencies {
	implementation 'com.github.green-solver:green:0.2.0'
	implementation 'commons-io:commons-io:2.6'
	implementation 'org.ow2.asm:asm:7.2'
	implementation 'org.ow2.asm:asm-commons:7.2'
	implementation 'org.ow2.asm:asm-util:7.2'
	implementation 'com.lmax:disruptor:3.4.2'
	implementation 'org.apache.logging.log4j:log4j-api:2.10.0'
	implementation 'org.apache.logging.log4j:log4j-core:2.10.0'
	testImplementation 'junit:junit:4.12'
	testImplementation 'org.slf4j:slf4j-nop:1.7.25'
	// implementation files(org.gradle.internal.jvm.Jvm.current().toolsJar)
}

//------------------------------------------------------------------------------
// Settings for generation of compile-time information.
//------------------------------------------------------------------------------

gversion {
	srcDir = 'src/main/java/'
	classPackage = 'za.ac.sun.cs.coastal'
	className = 'CompileInfo'
}

project.compileJava.dependsOn(createVersionFile)

//------------------------------------------------------------------------------
// Settings for using the Eclipse compiler.
// Currently not activated.
// This version number comes from clicking on "About" in Eclipse, and then
// clicking on "Eclipse Java Development Tools".
//------------------------------------------------------------------------------

// ecj.toolVersion = '3.18.0'

//------------------------------------------------------------------------------
// Put the ".properties" files in the same directory as the ".class" files.
//------------------------------------------------------------------------------

sourceSets {
	test {
		output.resourcesDir = 'build/classes/java/test'
	}
}

//------------------------------------------------------------------------------
// Chosen version of https://checkstyle.org/
//------------------------------------------------------------------------------

checkstyle {
	toolVersion '8.1'
}

//------------------------------------------------------------------------------
// Fine-tuning for javadoc.
//------------------------------------------------------------------------------

javadoc {
	destinationDir = file('build/docs/api')
	options.docletpath = [ file('build/classes/java/main') ]
	options.doclet = 'za.ac.sun.cs.coastal.utility.CoastalDoclet'
	options.noQualifiers = [ 'java.lang.*', 'java.io.*', 'java.util.*', 'org.apache.*' , 'za.ac.sun.cs.coastal.' ]
	options.memberLevel = JavadocMemberLevel.PRIVATE
}

//------------------------------------------------------------------------------
// Fine-tuning for installation scripts.
//------------------------------------------------------------------------------

applicationDefaultJvmArgs = [ '-ea' ]

startScripts {
	doLast {
		unixScript.text = unixScript.text.replace('COASTAL_APP_HOME', '\$APP_HOME')
		windowsScript.text = windowsScript.text.replace('COASTAL_APP_HOME', '%~dp0..')
	}
}

//------------------------------------------------------------------------------
// Enforce gradle version for Travis CI
//------------------------------------------------------------------------------

wrapper {
	gradleVersion = '6.0.1'
}

//------------------------------------------------------------------------------
// Publication settings
//------------------------------------------------------------------------------

java {
	withSourcesJar()
    withJavadocJar()
}
	
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			groupId = group
			pom {
				name = 'COASTAL'
				description = 'Concolic analysis tool for Java'
				url = 'https://github.com/DeepseaPlatform/coastal'
				packaging = 'jar'
				organization {
					name = 'DeepseaPlatform'
					url = 'https://github.com/DeepseaPlatform'
				}
				issueManagement {
					system = 'GitHub'
					url = 'https://github.com/DeepseaPlatform/coastal/issues'
				}
				licenses {
					license {
						name = 'Apache License, Version 2.0'
						url = 'https://www.apache.org/licenses/LICENSE-2.0'
						distribution = 'repo'
					}
				}
				scm {
					url = 'https://github.com/DeepseaPlatform/coastal'
					connection = 'scm:git:git@github.com:DeepseaPlatform/coastal.git'
					developerConnection = 'scm:git:git@github.com:DeepseaPlatform/coastal.git'
				}
				developers {
					developer {
						id = 'jacogeld'
						name = 'Jaco Geldenhuys'
						email = 'jacogeld@gmail.com'
					}
					developer {
						id = 'wvisser'
						name = 'Willem Visser'
					}
				}
			}
		}
	}
	repositories {
		maven {
			credentials {
			 	username repo_username
			 	password repo_password
			}
			if (isReleaseVersion) {
				// url = "file://${buildDir}/repo"
				url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
			} else {
				// url = "file://${buildDir}/snap"
				url = 'https://oss.sonatype.org/content/repositories/snapshots'
			}
		}
	}
}

signing {
    if (isReleaseVersion) {
    	sign publishing.publications
    }
}
